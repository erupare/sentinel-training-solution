# This policy uses the tfstate import to restrict ACM certificates
# to have domains that are sub-domains of hashidemos.io

# Import the v2 tfstate import, but use the alias "tfstate"
import "tfstate/v2" as tfstate

# Get all AWS ACM certs
allACMCerts = filter tfstate.resources as address, r {
  r.type is "aws_acm_certificate" and
  r.mode is "data"
}

# Filter to ACM certs that are not sub-domains of hashidemos.io
violatingACMCerts = filter allACMCerts as address, cert {
  cert.values.domain else "" not matches "(.+)\\.hashidemos\\.io$" and
  print(address, "has domain", cert.values.domain,
        "that is not a sub-domain of hashidemos.io")
}

# Count violations
violations = length(violatingACMCerts)

# Main rule that evaluates the result of the validation function
main = rule {
  violations is 0
}
